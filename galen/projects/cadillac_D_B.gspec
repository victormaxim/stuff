@objects
navbar_item-*       .nav_bar td .nav_item td td
caddy_logo          .bowtie
caddy_logo_img      .bowtie>a>img
masthead_img        .masthead_img_full img
app_icon-*          .app_icon

container_table-*   .container_module
navbar_table        .nav_bar

contact-*           .contact_inner_content
social_icons-*      .inner_content tbody tr td table:nth-child(3) tbody td table a img

# navbar_b-*          .nav_bar .nav_item a
# .inner_content tbody tr td table:nth-child(3) tbody td table:nth-child(5) img
# navbar_item-*       xpath   (//comment()[contains(.,'Inicio nav')])[1]/following-sibling::table[1]/tbody/tr/td[2]/table
# masthead            xpath   (//comment()[contains(.,'Inicio Masthead')])/following-sibling::*[1]

@groups
    tables     container_table-*, navbar_table

@set
    desktopWidth 650px
    mobileWidth 320 to 334px

= Navbar =
"check that top Nav Bar items are displayed and aligned on mobile and absent for mobile"
    navbar_item-*:
        @on desktop
            visible
        @on mobile
            absent

    @forEach [navbar_item-*] as navBar, next as nextNavBar
        ${navBar}:
            @on desktop
                left-of ${nextNavBar} 0px
                aligned horizontally all ${nextNavBar}

= App Icons =
    app_icon-*:
        visible

= Social Icons =
    @forEach [social_icons-*] as socialIcon, next as nextSocialIcon
        ${socialIcon}:
            @on desktop
                left-of ${nextSocialIcon} 12px
                aligned horizontally all ${nextSocialIcon}
    @for [1-3] as index
        social_icons-${index}:
            @on mobile
                left-of social_icons-${index + 1} 20px


= Logo =
    caddy_logo_img:
        @on desktop
            image file imgs/logo.jpg, error 12px
        @on mobile
            image file imgs/logo_mobile.jpg, error 12px


= Masthead =
    masthead_img:
        @on desktop
            width ${desktopWidth}
        @on mobile
            width ${mobileWidth}

= Tables =
    container_table-*:
        @on desktop
            width ${desktopWidth}
        @on mobile
            width ${mobileWidth}
    navbar_table:
        @on desktop
            width ${desktopWidth}
        @on mobile
            absent
    @forEach [&modules] as module, next as nextModule
        ${module}:
            @on desktop
            aligned vertically all ${nextModule}