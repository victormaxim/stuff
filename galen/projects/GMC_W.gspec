@objects
#    menu_item-*    body>table>tbody>tr>td>table:nth-child(2) td
    menu_item-*     xpath   (//a[contains(@description, 'GMC')]/img)/../../../td

    gmc_logo        //a[contains(@description, 'GMC')]/img

    CTA_button      xpath   //a[contains(@description, 'F')]/img[contains(@src, 'cta')]
# bottom menus count varies. so it selects the above td for Locate a vehicle (which surely is present) as parent, then all its children
    bottom_menu-*   xpath   (//img[contains(@alt, 'Locate')])/../../../td
    tables-*        body>table>tbody>tr>td:nth-of-type(2)>table
    social_icons-*  xpath   //a/img[contains(@src, 'social')]

@set
    desktopWidth 498px
    mobileWidth 320px


= Top menu items: visibility and alignment =
    @forEach [menu_item-*] as menuItem, next as nextMenu
        ${menuItem}:
            @on desktop
                visible
                left-of ${nextMenu} 0px
                aligned horizontally all ${nextMenu}

= GMC Logo: image comparison =
    gmc_logo:
        @on desktop
            image file imgs/bowtie_gmc.jpg, error 20%, tolerance 99

        @on mobile
            image file imgs/bowtie_gmc.jpg, error 20%, tolerance 99

= Call to action button: exists on all templates =
    CTA_button:
        visible

= LAV, LAD, Chat: visibility, alignment =
    @forEach [bottom_menu-*] as menuItem, next as nextMenu
        ${menuItem}:
            @on desktop
                visible
                left-of ${nextMenu} 0px
                aligned horizontally all ${nextMenu}

= Social icons: visibility and alignment =
    @forEach [social_icons-*] as social, next as nextSocial
        ${social}:
            @on desktop
                visible
                left-of ${nextSocial} 37px
                aligned horizontally all ${nextSocial}

= Tables: width and alignment =

    @forEach [tables-*] as table, next as nextTable
        ${table}:
            @on desktop
                visible
                width ${desktopWidth}
                aligned vertically all ${nextTable}
